// Copyright 2020 Autodesk
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

global string $stageFromExistingLayer_primPath = "";
global string $stageFromExistingLayer_excludePrimPath = "";
global int $stageFromExistingLayer_loadPayloads = 1;

// stageFromExistingLayer_UISetup
// creates the options of the stageFromExistingLayer dialog
global proc stageFromExistingLayer_UISetup(string $parent) {
    setParent $parent;
    $parent = `scrollLayout -childResizable true`;

    frameLayout -label "USD Layer Options";
    textFieldGrp -l "Prim Path:" primPathField;
    textFieldGrp -l "Exclude Prim Path:" excludePrimPathField;
    checkBoxGrp -l "Load Payloads:" loadPayloadsCheckBox;
}

// stageFromExistingLayer_UIInit
// init defaults values for the options of the stageFromExistingLayer dialog
global proc stageFromExistingLayer_UIInit(string $parent, string $filterType) {
    global string $stageFromExistingLayer_primPath;
    global string $stageFromExistingLayer_excludePrimPath;
    global int $stageFromExistingLayer_loadPayloads;

    setParent $parent;
    textFieldGrp -e -text $stageFromExistingLayer_primPath primPathField;
    textFieldGrp -e -text $stageFromExistingLayer_excludePrimPath excludePrimPathField;
    checkBoxGrp -e -value1 $stageFromExistingLayer_loadPayloads loadPayloadsCheckBox;
}

global proc stageFromExistingLayer_UICommit(string $parent, string $selectedFile) {
    global string $stageFromExistingLayer_primPath;
    global string $stageFromExistingLayer_excludePrimPath;
    global int $stageFromExistingLayer_loadPayloads;

    setParent $parent;
    // fetch values
    $stageFromExistingLayer_primPath = `textFieldGrp -q -text  primPathField`;
    $stageFromExistingLayer_excludePrimPath = `textFieldGrp -q -text  excludePrimPathField`;
    $stageFromExistingLayer_loadPayloads =  `checkBoxGrp -q -value1 loadPayloadsCheckBox`;

    // actually load the file
    string $fileName = $selectedFile;
    string $baseName = capitalizeString(`basenameEx $fileName`);
    if( isValidObjectName($baseName) )
        $baseName += "_usd";
    else
        $baseName = "UsdStage";

    string $shapeNode = `createNode "mayaUsdProxyShape" -skipSelect -name ($baseName+"Shape")`;
    setAttr -type "string" ($shapeNode+".filePath") $fileName;
    setAttr -type "string" ($shapeNode+".primPath") $stageFromExistingLayer_primPath;
    setAttr -type "string" ($shapeNode+".excludePrimPaths") $stageFromExistingLayer_excludePrimPath;
    setAttr ($shapeNode+".loadPayloads") $stageFromExistingLayer_loadPayloads;
    connectAttr time1.outTime ($shapeNode+".time");
}

global proc mayaUsd_createStagesFromExistingLayer() {
    fileDialog2 
    -fileMode 1
    -caption "Create USD Stage From Existing Layer"
    -fileFilter "All USD Files (*.usd *.usda *.usdc *.usdz);;*.usd;;*.usda;;*.usdc;;*.usdz"
    -okCaption "Create"
    -optionsUICreate "stageFromExistingLayer_UISetup"
    -optionsUIInit "stageFromExistingLayer_UIInit"
    -optionsUICommit2 "stageFromExistingLayer_UICommit";
}




