// Copyright 2020 Autodesk
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

proc setOptionVars(int $forceFactorySettings)
{
    if ($forceFactorySettings || !`optionVar -exists stageFromFile_primPath`) {
        optionVar -stringValue stageFromFile_primPath "";
    }

    if ($forceFactorySettings || !`optionVar -exists stageFromFile_excludePrimPath`) {
        optionVar -stringValue stageFromFile_excludePrimPath "";
    }

    if ($forceFactorySettings || !`optionVar -exists stageFromFile_loadPayloads`) {
        optionVar -intValue stageFromFile_loadPayloads 1;
    }
}

proc setLatestLoadStageFolder( string $sceneFolder )
{
    optionVar -stringValue mayaUsd_LatestLoadStageFolder $sceneFolder;
}

proc string getLatestLoadStageFolder()
{
    string $sceneFolder;

    // First check if we've saved a location in the option var
    if (`optionVar -exists mayaUsd_LatestLoadStageFolder`)
    {
        $sceneFolder = `optionVar -q mayaUsd_LatestLoadStageFolder`;
    }

    // Then check if there is a current Maya scene, if so choose that as
    // a starting point.
    if ("" == $sceneFolder)
    {
        $sceneFolder = dirname(`file -q -sceneName`);
    }

    // If we are really starting from scratch then just go with the 
    // current workspace location for scenes.
    if ("" == $sceneFolder)
    {
        string $workspaceLocation = `workspace -q -fn`;
        string $scenesFolder = `workspace -q -fileRuleEntry "scene"`;
        $sceneFolder = $workspaceLocation + "/" + $scenesFolder;
    }

    return $sceneFolder;
}

// stageFromFile_UISetup
// creates the options of the stageFromFile dialog
global proc string stageFromFile_UISetup(string $parent) {
    setParent $parent;
    string $layout = `scrollLayout -childResizable true`;

    $title = `getMayaUsdString("kUsdFileOptions")`;
    $frame = `frameLayout -label $title -collapsable false`;
    checkBoxGrp -l `getMayaUsdString("kLoadPayloads")` 
        -ann `getMayaUsdString("kLoadPayloadsAnn")`
        -sbm `getMayaUsdString("kLoadPayloadsSbm")`
        loadPayloadsCheckBox;

    frameLayout -label `getMayaUsdString("kLabelStageDisplay")` -parent $frame -collapsable false;
    textFieldGrp -l `getMayaUsdString("kPrimPath")` 
        -ann `getMayaUsdString("kPrimPathAnn")`
        -sbm `getMayaUsdString("kPrimPathSbm")`
        primPathField;
    textFieldGrp -l `getMayaUsdString("kExcludePrimPaths")` 
        -ann `getMayaUsdString("kExcludePrimPathsAnn")`
        -sbm `getMayaUsdString("kExcludePrimPathsSbm")`
        excludePrimPathField;

    return $layout;
}

// stageFromFile_UIInit
// init defaults values for the options of the stageFromFile dialog
global proc stageFromFile_UIInit(string $parent, string $filterType) {
    setOptionVars(false);

    string $ppath = `optionVar -q stageFromFile_primPath`;
    string $exppath = `optionVar -q stageFromFile_excludePrimPath`;
    int $loadp = `optionVar -q stageFromFile_loadPayloads`;

    setParent $parent;
    textFieldGrp -e -text $ppath primPathField;
    textFieldGrp -e -text $exppath excludePrimPathField;
    checkBoxGrp -e -value1 $loadp loadPayloadsCheckBox;
}

global proc stageFromFile_UICommit(string $parent) {
    setParent $parent;

    // fetch values
    optionVar -stringValue stageFromFile_primPath
        (`textFieldGrp -q -text  primPathField`);
    optionVar -stringValue stageFromFile_excludePrimPath
        (`textFieldGrp -q -text  excludePrimPathField`);
    optionVar -intValue stageFromFile_loadPayloads
        (`checkBoxGrp -q -value1 loadPayloadsCheckBox`);
}

proc string doCreateStage(string $fileName) {
    // actually load the file
    string $baseName = basenameEx($fileName);
    if( ! isValidObjectName($baseName) )
        $baseName = "UsdStage";

    string $ppath = `optionVar -q stageFromFile_primPath`;
    string $exppath = `optionVar -q stageFromFile_excludePrimPath`;
    int $loadp = `optionVar -q stageFromFile_loadPayloads`;

    string $shapeNode = `createNode "mayaUsdProxyShape" -skipSelect -name ($baseName+"Shape")`;
    setAttr -type "string" ($shapeNode+".filePath") $fileName;
    setAttr -type "string" ($shapeNode+".primPath") $ppath;
    setAttr -type "string" ($shapeNode+".excludePrimPaths") $exppath;
    setAttr ($shapeNode+".loadPayloads") $loadp;
    connectAttr time1.outTime ($shapeNode+".time");
    select -r $shapeNode;
    string $fullPath[] = `ls -l $shapeNode`;
    return $fullPath[0];
}

global proc createStageFromExistingCallback(string $parent, int $doIt)
{
    stageFromFile_UICommit($parent);

    if ($doIt) {
        mayaUsd_createStageFromFile; 
        addToRecentCommandQueue "mayaUsd_createStageFromFile" 
            "mayaUsdCreateStageFromFile";
    }
}

global proc createStageFromExistingSetup(string $parent, int $forceFactorySettings)
{
    setOptionVars($forceFactorySettings);

    stageFromFile_UIInit($parent, "");
}

global proc mayaUsd_createStageFromFileOptions()
{
    global int $gOptionBoxTemplateFrameSpacing;

    setOptionVars(false);

    string $commandName = "createStageFromExisting";
    string $callback = ($commandName + "Callback");
    string $setup = ($commandName + "Setup");

    string $layout = getOptionBox();
    setParent $layout;

    setOptionBoxCommandName($commandName);

    setUITemplate -pushTemplate DefaultTemplate;

    string $parent = `formLayout createStageFromExistingOptions`;

    string $settingsFrame = stageFromFile_UISetup($parent);

    formLayout -e
        -af $settingsFrame "top" $gOptionBoxTemplateFrameSpacing
        -af $settingsFrame "left" $gOptionBoxTemplateFrameSpacing
        -af $settingsFrame "right" $gOptionBoxTemplateFrameSpacing
        -af $settingsFrame "bottom"  $gOptionBoxTemplateFrameSpacing
        $parent;

    string $applyCloseBtn = getOptionBoxApplyAndCloseBtn();
    button -edit -label `getMayaUsdString("kCreateStageFromFile")`
        -command ($callback + " " + $parent + " " + 1 + "; hideOptionBox;")
        $applyCloseBtn;

    string $applyBtn = getOptionBoxApplyBtn();
    button -edit -label `getMayaUsdString("kSaveAndClose")`
        -command ($callback + " " + $parent + " " + 0 + "; hideOptionBox;")
        $applyBtn;

    string $cancelBtn = getOptionBoxCloseBtn();
    button -edit -label `getMayaUsdString("kCancel")`
        -command ("hideOptionBox;")
        $cancelBtn;

    string $resetBtn = getOptionBoxResetBtn();
    button -edit 
        -command ($setup + " " + $parent + " " + 1)
        $resetBtn;

    string $saveBtn = getOptionBoxSaveBtn();
    button -edit 
        -command ($callback + " " + $parent + " " + 0)
        $saveBtn;

    setOptionBoxTitle(getMayaUsdString("kCreateUsdStageFromFileOptions"));

    setOptionBoxHelpTag( "" );

    eval (($setup + " " + $parent + " " + 0));	

    showOptionBox();
}

global proc mayaUsd_createStageFromFile() {
    setOptionVars(false);
    
    $caption = getMayaUsdString("kCreateUsdStageFromFile");
    $fileFilter = getMayaUsdString("kAllUsdFiles") + " (*.usd *.usda *.usdc *.usdz);;*.usd;;*.usda;;*.usdc;;*.usdz";
    $okCaption = getMayaUsdString("kCreateStage");
    
    string $startFolder = getLatestLoadStageFolder();

    string $result[] = `fileDialog2 
        -fileMode 1
        -caption $caption
        -fileFilter $fileFilter 
        -okCaption $okCaption
        -dir $startFolder
        -optionsUICreate "stageFromFile_UISetup"
        -optionsUIInit "stageFromFile_UIInit"
        -optionsUICommit "stageFromFile_UICommit"`;

    if (size($result) > 0) {
        setLatestLoadStageFolder( dirname($result[0]) );
        doCreateStage($result[0]);
    }
}

global proc string mayaUsd_createStageFromFilePath(string $usdFile)
{
    return doCreateStage($usdFile);
}
