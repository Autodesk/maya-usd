// Copyright 2021 Autodesk
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

proc string expandToSelection(string $obj)
{
    // By default, for example if the obj is empty, we want to use the selection.
    int $expandToSel = 1;

    // If obj is non-empty, expand it to the selection if it is part of the selection.
    if (size($obj)) {
        string $script = "import maya.internal.ufeSupport.utils as ufeUtils; selection = ufeUtils.getNonMayaSelectedItems(); inSel = ['" + $obj + "'.startswith(sel) for sel in selection]; selection and sum(inSel)";
        $expandToSel = `python($script)`;
    }

    // If we expand to selection, use the UFE selection.
    if ($expandToSel) {
        string $nonMayaObjs[] = `python("import maya.internal.ufeSupport.utils as ufeUtils; ufeUtils.getNonMayaSelectedItems()")`;
        $obj = $nonMayaObjs[0];
    }

    return $obj;
}

proc int canEditAsMaya(string $obj)
{
    if (!hasPrimUpdater())
        return 0;

    if (size($obj) != 0) {
        return `python("from mayaUsd.lib import PrimUpdaterManager; PrimUpdaterManager.canEditAsMaya('" + $obj + "')")`;
    }
    return 0;
}

global proc mayaUsdMenu_editAsMaya(string $obj)
{
    if (!hasPrimUpdater())
        return;

    if (size($obj) != 0) {
        mayaUsdEditAsMaya $obj;
    }
}

global proc mayaUsdMenu_duplicate(string $ufePath)
{
    if (!hasPrimUpdater())
        return;
        
    if (size($ufePath)) {
        mayaUsdDuplicate $ufePath "|world";
    }
}

global proc USDMenuProc(string $parent, string $obj)
{
    if (!hasPrimUpdater())
        return;
        
    $obj = expandToSelection($obj);
    if (size($obj) != 0)
    {
        popupMenu -e -dai $parent;
        setParent -menu $parent;

        setParent -menu ..;
        if (canEditAsMaya($obj)) {
            // Temporary - hide this context menu items behind an
            //             env var until it is completed.
            string $s = `getenv "MAYAUSD_ENABLE_EDIT_AS_MAYA_DATA"`;
            string $ls = tolower($s);
            if (`match $ls "true|yes|on|1"` !="")
            {
                menuItem -label "Edit As Maya Data" -image "edit_as_Maya.png" -command ("mayaUsdMenu_editAsMaya \"" + $obj + "\"");
            }
        }
        menuItem -label "Duplicate As Maya Data" -command ("mayaUsdMenu_duplicate \"" + $obj + "\"");
    }
}
