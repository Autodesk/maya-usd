// Copyright 2021 Autodesk
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

int $_hasPrimUpdater = -1;

global proc int hasPrimUpdater()
{
    global int $_hasPrimUpdater;
    if ($_hasPrimUpdater < 0) {
        $_hasPrimUpdater = `python("def checkPrimUp():\n  try:\n    from mayaUsd.lib import PrimUpdaterManager;\n    return 1;\n  except:\n    return 0;\ncheckPrimUp()")`;
    }
    return $_hasPrimUpdater;
}

proc int canEditAsMaya(string $obj)
{
    if (!hasPrimUpdater())
        return 0;

    if (size($obj) == 0) {
        string $nonMayaObjs[] = `python("import maya.internal.ufeSupport.utils as ufeUtils; ufeUtils.getNonMayaSelectedItems()")`;
        $obj = $nonMayaObjs[0];
    }
    if (size($obj) != 0) {
        return `python("from mayaUsd.lib import PrimUpdaterManager; PrimUpdaterManager.canEditAsMaya('" + $obj + "')")`;
    }
    return 0;
}

global proc mayaUsdMenu_pullToDG(string $obj)
{
    if (!hasPrimUpdater())
        return;

    if (size($obj) == 0) {
        string $nonMayaObjs[] = `python("import maya.internal.ufeSupport.utils as ufeUtils; ufeUtils.getNonMayaSelectedItems()")`;
        $obj = $nonMayaObjs[0];
    }
    if (size($obj) != 0) {
        python("from mayaUsd.lib import PrimUpdaterManager; import ufe; PrimUpdaterManager.editAsMaya('" + $obj + "');");
    }
}

global proc mayaUsdMenu_duplicateToDG( string $ufePath )
{
    if (!hasPrimUpdater())
        return;
        
    if (size($ufePath) == 0) {
        string $nonMayaObjs[] = `python("import maya.internal.ufeSupport.utils as ufeUtils; ufeUtils.getNonMayaSelectedItems()")`;
        $ufePath = $nonMayaObjs[0];
    }
    
    if (size($ufePath)) {
        python("from mayaUsd.lib import PrimUpdaterManager; import maya.cmds as cmds; PrimUpdaterManager.duplicate('" + $ufePath + "', '|world');");
    }
}
global proc USDMenuProc(string $parent, string $obj)
{
    if (!hasPrimUpdater())
        return;
        
    int $haveNonMaya = `python("import maya.internal.ufeSupport.utils as ufeUtils; ufeUtils.hasNonMayaSelectedItems()")`;
    if ($haveNonMaya)
    {
        popupMenu -e -dai $parent;
        setParent -menu $parent;

        setParent -menu ..;
        if (canEditAsMaya($obj)) {
            menuItem -label "Edit As Maya Data" -image "edit_as_Maya.png" -command ("mayaUsdMenu_pullToDG \"" + $obj + "\"");
        }
        menuItem -label "Duplicate As Maya Data" -command ("mayaUsdMenu_duplicateToDG \"" + $obj + "\"");
    }
}
